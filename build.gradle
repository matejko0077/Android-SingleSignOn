import com.github.spotbugs.SpotBugsTask

buildscript {
    ext {
        kotlin_version = '1.3.72'
    }
    repositories {
        google()
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.6'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.8.0"
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.github.spotbugs"
apply plugin: "io.gitlab.arturbosch.detekt"

group = 'com.github.nextcloud'

spotbugs {
    toolVersion = '3.1.12'
}

configurations {
    ktlint
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName '1.0'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        // translations are imported from transifex, so no need to check here
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        disable 'MissingQuantity'
        disable 'InconsistentArrays'
        disable 'TypographyEllipsis'
        disable 'GradleDependency'
        disable 'VectorPath'
        disable 'IconMissingDensityFolder'
        disable 'IconDensities'
        abortOnError false
        htmlReport true
        htmlOutput file("$project.buildDir/reports/lint/lint.html")
    }
    productFlavors {
    }

    tasks.register("spotbugsDebug", SpotBugsTask) {
        ignoreFailures = false
        effort = "max"
        reportLevel = "medium"
        classes = fileTree("$project.buildDir/intermediates/javac/debug/classes/")
        excludeFilter = file("${project.rootDir}/spotbugs-filter.xml")
        pluginClasspath = project.configurations.spotbugsPlugins
        source = fileTree('src/main/java')
        classpath = files()
        include '**/*.java'
        exclude '**/gen/**'

        reports {
            xml.enabled = false
            html.enabled = true
            html {
                destination = file("$project.buildDir/reports/spotbugs/spotbugs.html")
            }
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "--reporter=plain", "--reporter=plain,output=${buildDir}/ktlint.txt,src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

detekt {
    reports {
        xml {
            enabled = false
        }
    }
    config = files("detekt.yml")
    input = files("src/")
}

dependencies {

    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.core:core:1.2.0'
    implementation 'androidx.fragment:fragment:1.2.4'

    compileOnly "org.projectlombok:lombok:1.18.12"
    annotationProcessor "org.projectlombok:lombok:1.18.12"

    api 'com.google.code.gson:gson:2.8.6'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'

    implementation 'commons-io:commons-io:2.6'

    implementation ('com.squareup.retrofit2:retrofit:2.6.4') {
        because '2.7.0 requires okhttp:3.14.x which requires API level 19'
    }
    implementation ('com.squareup.okhttp3:okhttp:3.12.11') {
        because '3.13+ requires Lollipop, but our minSdkVersion is 14'
    }

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'
    spotbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:7.4.7'

    ktlint "com.pinterest:ktlint:0.36.0"

    // Required for local unit tests (JUnit 4 framework)
    testImplementation 'junit:junit:4.13'
    // required if you want to use Mockito for unit tests
    testImplementation 'org.mockito:mockito-core:3.3.3'
}